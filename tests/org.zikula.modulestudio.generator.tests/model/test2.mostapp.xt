/**
 * Copyright (c) 2007-2025 Axel Guckelsberger
 */

/* XPECT_SETUP org.zikula.modulestudio.generator.tests.MostGeneratorTest END_SETUP */

/*
test which files are contained in the output
XPECT files ---
    .gitignore
    composer.json
--- */

application "SimpleNews" targets SF74 {
    documentation "Simple news extension"
    vendor "Guite"
    author "Axel Guckelsberger"
    email "info@guite.de"
    url "https://guite.de"
    version "0.0.5"
    license "My license"
    skipFiles "foo, bar"
    markFiles "foo, bar"
    entities {
        entity "article" leading tree loggable {
            nameMultiple "articles"
            displayPattern "#title#"
            lockType OPTIMISTIC
            approval true
            fields {
                string "title" {
                    sluggablePosition 1
                    length 200
                },
                number "currentVersion" {
                    role VERSION
                },
                datetime "myPast" past,
                datetime "myFuture" future
            }
            actions {
                indexAction "Index" {
                    documentation "My index action"
                },
                detailAction "Detail",
                editAction "Edit",
                deleteAction "Delete"
            }
        },
        entity "image" loggable geographical {
            nameMultiple "images"
            displayPattern "#title#"
            lockType OPTIMISTIC
            fields {
                string "title" translatable {
                    sluggablePosition 1
                    length 200
                },
                string "zipCode" {
                    mandatory false
                },
                text "description",
                number "currentVersion" unique {
                    role VERSION
                },
                upload "myImage",
                datetime "myStart" startDate,
                datetime "myEnd" endDate,
                datetime "myPast" past {
                    role TIME
                },
                datetime "myFuture" future {
                    role TIME
                }
            }
            actions {
                indexAction "Index",
                detailAction "Detail",
                editAction "Edit"
            }
        },
        entity "location" geographical {
            nameMultiple "locations"
            displayPattern "#title#"
            fields {
                string "title" {
                    visibleOnIndex false
                    visibleOnDetail false
                    visibleOnSort false
                },
                string "myPassword" {
                    visibleOnDetail false
                    visibleOnSort false
                    role PASSWORD
                },
                string "zipCode" unique {
                    mandatory false
                    visibleOnIndex false
                    visibleOnSort false
                },
                list "myList1" {
                    items {
                        listItem "My first item",
                        default listItem "My second item"
                    }
                },
                list "myList2" expanded {
                    items {
                        listItem "My first item",
                        default listItem "My second item"
                    }
                },
                list "myList3" multiple {
                    items {
                        listItem "My first item",
                        default listItem "My second item"
                    }
                },
                list "myList4" multiple expanded {
                    min 1
                    max 2
                    items {
                        default listItem "My first item",
                        default listItem "My second item"
                    }
                },
                datetime "myTime" immutable {
                    defaultValue "now"
                    role TIME
                },
                datetime "myStart" startDate {
                    role DATE
                },
                datetime "myEnd" endDate {
                    role DATE
                },
                datetime "myPast" past {
                    role DATE
                },
                datetime "myFuture" future {
                    role DATE
                },
                upload "myImage",
                user "myUser"
            }
            actions {
                indexAction "Index",
                detailAction "Detail",
                editAction "Edit",
                customAction "Export"
            }
        }
    }
    relations {
        one2many bidirectional {
            source "article"
            sourceAlias "article"
            target "image"
            targetAlias "images"
            sourceEditing EMBEDDED
            targetEditing NONE
            cascade ALL
            orderBy "currentVersion"
            minTarget 1
            maxTarget 3
        },
        many2one unique {
            source "article"
            sourceAlias "myArticles"
            target "image"
            targetAlias "myImage"
            nullable false
            expandedSource true
            expandedTarget true
            autocompleteSource true
            autocompleteTarget true
            fetchType EAGER
            sourceEditing INLINE
        },
        many2many bidirectional {
            source "image"
            sourceAlias "images"
            target "location"
            targetAlias "locations"
            sourceEditing CHOOSE
            targetEditing INLINE
            refClass "imageLocation"
            orderBy "title"
            orderByReverse "currentVersion"
            minSource 1
            maxSource 3
            minTarget 1
            maxTarget 3
        }
    }
    variables {
        varContainer "GeneralSettings" {
            documentation "My variable section"
            fields {
                user "myUser1",
                upload "myImage"
            }
        },
        varContainer "OtherSettings" {
            documentation "My variable section with {{ variable }}"
            sortOrder 3
            fields {
                user "myUser2",
                number "myNumber" {
                    numberType DECIMAL
                },
                datetime "myDate" {
                    role DATE
                },
                datetime "myStart" startDate,
                datetime "myEnd" endDate
            }
        }
    }
}
