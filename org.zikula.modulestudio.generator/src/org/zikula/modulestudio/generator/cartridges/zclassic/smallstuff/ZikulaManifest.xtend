package org.zikula.modulestudio.generator.cartridges.zclassic.smallstuff

import com.google.inject.Inject
import de.guite.modulestudio.metamodel.modulestudio.Application
import org.eclipse.xtext.generator.IFileSystemAccess
import org.zikula.modulestudio.generator.extensions.FormattingExtensions
import org.zikula.modulestudio.generator.extensions.NamingExtensions
import org.zikula.modulestudio.generator.extensions.Utils

class ZikulaManifest {
    @Inject extension FormattingExtensions = new FormattingExtensions
    @Inject extension NamingExtensions = new NamingExtensions
    @Inject extension Utils = new Utils

    def generate(Application it, IFileSystemAccess fsa) {
        if (targets('1.3.5')) {
            return
        }
        var fileName = 'zikula.manifest.json'
        if (!shouldBeSkipped(getAppSourcePath + fileName)) {
            if (shouldBeMarked(getAppSourcePath + fileName)) {
                fileName = 'zikula.manifest.generated.json'
            }
            fsa.generateFile(getAppSourcePath + fileName, manifestFile)
        }
    }

    def private manifestFile(Application it) '''
        {
            «manifestContent»
        }
    '''

    def private manifestContent(Application it) '''
        "vendor": {
            "title": "«vendor»",
            "url": "«url»",
            "logo": ""
        },
        "extension": {
            "name": "«name.formatForDisplayCapital»",
            "url": "«url»",
            "icon": ""
        },
        "version": {
            "semver": "«version»",
            "compatibility": ">=1.3.7",
            "composerpath": "composer.json",
            "description": "«IF documentation !== null && documentation != ''»«documentation.replace('"', "'")»«ELSE»«appName» module generated by ModuleStudio «msVersion».«ENDIF»",
            "keywords": [
            ]
        },
        "urls": {
            "version": "",
            "docs": "",
            "demo": "",
            "download": "",
            "issues": ""
        },
        "dependencies": [
        ]
    '''
}
