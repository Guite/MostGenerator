module org.zikula.modulestudio.generator.workflow.msWorkflow

/**
 * The inner processing workflow.
 */

import org.eclipse.emf.mwe.utils.DirectoryCleaner
import org.eclipse.emf.mwe.utils.FileCopy
import org.eclipse.emf.mwe.utils.StandaloneSetup
import org.eclipse.emf.mwe.core.container.IfComponent
import org.eclipse.xtend.check.CheckComponent
//import org.eclipse.xtext.generator.GeneratorComponent
import org.eclipse.xtext.mwe.Reader
import org.zikula.modulestudio.generator.cartridges.MostGeneratorSetup
import org.zikula.modulestudio.generator.cartridges.MostGeneratorSupport
import org.zikula.modulestudio.generator.workflow.components.DirectoryCreator
import org.zikula.modulestudio.generator.workflow.components.Logger
import org.zikula.modulestudio.generator.workflow.components.MostGeneratorComponent
import org.zikula.modulestudio.generator.workflow.components.StopWatch

// The model to be processed (file name without extension)
var modelName
// The path where to find the model, without trailing slash
var modelPath
// The generator cartridge to execute (zclassic, zoo, documentation, reporting)
var cartridgeName

// whether to validate the model before processing
var doValidation
// whether to copy the models into the target folder
var doModelCopy

// Destination folder
var targetDir

// Cartridge-specific destination folder
var targetDirCartridge = '${targetDir}${cartridgeName}'

// calculated from input vars
var modelFile = '${modelPath}/${modelName}.mostapp'
var diagramFile = '${modelPath}/${modelName}.mostdiagram'
var modelFileCopy = '${targetDir}/${modelName}.mostapp'
var diagramFileCopy = '${targetDir}/${modelName}.mostdiagram'

var mostSlot = 'mostModel'

Workflow {
    // set up EMF for standalone execution
    bean = StandaloneSetup {
        // URI's starting with 'platform:/resource/' are normalized to the (canonical) path to the platform URI
        //platformUri = '.'
        //registerGeneratedEPackage = 'org.eclipse.emf.ecore.EcorePackage'
        registerGeneratedEPackage = 'de.guite.modulestudio.metamodel.modulestudio.ModulestudioPackage'
    }

    // init time measurement
    component = StopWatch : stopWatch {}

    // create Guice module for generator
    component = MostGeneratorSupport {}

    component = Logger { message = 'Reading input file:' }
    component = Logger { message = modelFile }
    component = Reader {
        path = modelPath //modelFile
        // create Injector for generator module
        register = MostGeneratorSetup {}
        loadResource = {
            slot = mostSlot
        }
    }

    component = Logger { message = 'Applying validation.' }
    component = IfComponent {
        cond = doValidation
        if = {
            cond = doValidation
/*
            // check components for constraints
            component = CheckComponent {
                // define all used meta models
                metaModel = mmEMF
                metaModel = mmModuleStudio

                // provide check files
                checkFile = 'checks::main'
                checkFile = 'checks::model'
                checkFile = 'checks::controller'
                checkFile = 'checks::view'

                // on which part of the model the checks should work (our application with all children)
                emfAllChildrenSlot = mostSlot
/*
                expression = 'model'
                // expression = 'model.eAllContents.union({model})'
* /
            }
*/
        }
    }

    component = Logger { message = 'Creating output directory ${targetDirCartridge}.' }
    component = DirectoryCreator {
       directory = targetDirCartridge
    }

    component = Logger { message = 'Clearing output directory ${targetDirCartridge}.' }
    component = DirectoryCleaner {
       directory = targetDirCartridge
    }

    component = Logger { message = 'Copying model artifacts.' }
    component = IfComponent {
        cond = doModelCopy
        if = {
            cond = doModelCopy
            // copy original domain model into target folder
            component = FileCopy {
                sourceFile = modelFile
                targetFile = modelFileCopy
            }
            // copy diagram model into target folder
            component = FileCopy {
                sourceFile = diagramFile
                targetFile = diagramFileCopy
            }
        }
    }

    // start generator component
    component = Logger { message = 'Starting generator cartridge:' }
    component = Logger { message = cartridgeName }
    component = MostGeneratorComponent {
        // create Injector for generator module
        register = MostGeneratorSetup {}
        slot = mostSlot
        cartridge = cartridgeName
        // specify where to put the generated code
        outlet = {
            path = '${targetDirCartridge}/${modelName}/'
        }
    }

    // output required generation time
    component = stopWatch
}
