package org.zikula.modulestudio.generator.cartridges.symfony.smallstuff

import de.guite.modulestudio.metamodel.Application
import org.zikula.modulestudio.generator.application.IMostFileSystemAccess
import org.zikula.modulestudio.generator.cartridges.symfony.smallstuff.documents.License_GPL
import org.zikula.modulestudio.generator.cartridges.symfony.smallstuff.documents.License_LGPL
import org.zikula.modulestudio.generator.cartridges.symfony.smallstuff.techdocs.TechComplexity
import org.zikula.modulestudio.generator.cartridges.symfony.smallstuff.techdocs.TechStructure
import org.zikula.modulestudio.generator.extensions.FormattingExtensions
import org.zikula.modulestudio.generator.extensions.ModelExtensions
import org.zikula.modulestudio.generator.extensions.NamingExtensions
import org.zikula.modulestudio.generator.extensions.Utils

class Docs {

    extension FormattingExtensions = new FormattingExtensions
    extension ModelExtensions = new ModelExtensions
    extension NamingExtensions = new NamingExtensions
    extension Utils = new Utils

    /**
     * Entry point for bundle documentation.
     */
    def generate(Application it, IMostFileSystemAccess fsa) {
        var fileName = 'CHANGELOG.md'
        fsa.generateFile(fileName, Changelog)

        fileName = 'README.md'
        fsa.generateFile(fileName, Readme)

        val docPath = getAppDocPath
        fsa.generateFile(docPath + 'help/en/' + fileName, Readme)

        fileName = 'credits.md'
        fsa.generateFile(docPath + fileName, Credits)

        fileName = 'modulestudio.md'
        fsa.generateFile(docPath + fileName, MostText)

        fileName = 'install.md'
        fsa.generateFile(docPath + fileName, Install)

        fileName = 'translation.md'
        fsa.generateFile(docPath + fileName, Translation)

        fileName = 'LICENSE'
        fsa.generateFile(getAppLicencePath + fileName, License)

        if (writeModelToDocs) {
            fsa.generateFile(docPath + '/model/.htaccess', htAccessForModel)
        }

        if (generateTechnicalDocumentation) {
            val techDocPath = docPath + '/model/'
            for (language : #['en', 'de']) {
                fileName = 'structure_' + language + '.html'
                fsa.generateFile(techDocPath + fileName, new TechStructure().generate(it, language))

                fileName = 'complexity_' + language + '.html'
                fsa.generateFile(techDocPath + fileName, new TechComplexity().generate(it, language))
            }
        }
    }

    def private Credits(Application it) '''
        # Credits

    '''

    def private Changelog(Application it) '''
        # Changelog

        Changes in «appName» «version»
    '''

    def private MostText(Application it) '''
        # ModuleStudio
        
        This Symfony bundle has been generated by ModuleStudio «msVersion», a model-driven solution
        for creating web applications for the Symfony and Zikula frameworks.

        If you are interested in how it works, visit <«msUrl»>.
    '''

    def private Install(Application it) '''
        # Installation and setup instructions

        ## Install the bundle

        Add an entry to your `composer.json` pointing to your repository like this:

        ```
          "repositories": [
            {
              "type": "git",
              "url": "https://github.com/«vendor.formatForCodeCapital»/«name.formatForCodeCapital»Bundle/"
            }
          ],
        ```

        Afterwards execute `composer require "«vendor.formatForCodeCapital»/«name.formatForCodeCapital»Bundle:dev-main"`.

        ## Setup

        ### When using the Flex recipe

        Just follow the instructions in your console.

        ### When not using Flex

        1. Add the bundle to `/config/bundles.php` adding `"«vendor.formatForCodeCapital»\\«name.formatForCodeCapital»Bundle\\«appName»": ["all"]`.
        2. Copy `Resources/docs/recipe/config/routes/«vendor.formatForDB»_«name.formatForDB».yaml` to `/config/routes/«vendor.formatForDB»_«name.formatForDB».yaml`.
        3. Create database tables
           1. Create a database migration using `php bin/console make:migration`.
           2. Review the generated migration file and execute it using `php bin/console doctrine:migrations:migrate`.

           Alternatively the quick and dirty way during development: `php bin/console doctrine:schema:update --force`.
        4. JavaScript routes  
          If you use the `FOSJsRoutingBundle` dump JS routes using `php bin/console fos:js-routing:dump`.
        5. Execute `php bin/console zikula:init-bundle «appName»`.
        6. Move or copy the directory `Resources/public/` to `/public/bundles/«appName.formatForDB»/`.
        7. Optional: dump the default configuration using `php bin/console config:dump-reference «appName»` and put it into `/config/packages/«vendor.formatForDB»_«name.formatForDB».yaml`.
        «IF hasUploads»
            8. Move or copy the directory `Resources/docs/recipe/public/uploads/«appName»` to `/public/uploads/«appName»/` and make it writable including all sub folders.
        «ENDIF»

        For questions and other remarks visit our homepage <«url»>.

        «ReadmeFooter»
    '''

    def private Translation(Application it) '''
        # Translation instructions

        To create a new translation follow the steps below:

        1. First install the bundle like described in the `install.md` file.
        2. Open a console and navigate to the Symfony root directory.
        3. Execute this command replacing `en` by your desired locale code:

        `php -dmemory_limit=2G bin/console translation:extract --bundle «appName» extension en`

        4. Translate the resulting `.yaml` files in `«relativeAppRootPath»/Resources/translations/`.

        For questions and other remarks visit our homepage <«url»>.

        «ReadmeFooter»
    '''

    def private ReadmeFooter(Application it) '''
        «author»«IF email != ''» («email»)«ENDIF»
        «IF url != ''»<«url»>«/*«ELSE»«msUrl»*/»«ENDIF»
    '''

    def Readme(Application it) '''
        # «appName» «version»

        «appDescription»

        This bundle is intended for being used with Symfony «targetSymfonyVersion» and later.

        For questions and other remarks visit our homepage <«url»>.

        «ReadmeFooter»
    '''

    def private License(Application it) '''
        «IF license == 'http://www.gnu.org/licenses/lgpl.html GNU Lesser General Public License'
          || license == 'GNU Lesser General Public License'
          || license == 'Lesser General Public License'
          || license == 'LGPL'»
            «new License_LGPL().generate(it)»
        «ELSEIF license == 'http://www.gnu.org/copyleft/gpl.html GNU General Public License'
          || license == 'GNU General Public License'
          || license == 'General Public License'
          || license == 'GPL'»
            «new License_GPL().generate(it)»
        «ELSE»
            Please enter your license text here.
        «ENDIF»
    '''

    def private htAccessForModel(Application it) '''
        # «generatedBy(it, timestampAllGeneratedFiles, versionAllGeneratedFiles)»
        # ------------------------------------------------------------
        # Purpose of file: block any web access to unallowed files
        # stored in this directory
        # ------------------------------------------------------------

        # Apache 2.2
        <IfModule !mod_authz_core.c>
            Deny from all
        </IfModule>

        # Apache 2.4
        <IfModule mod_authz_core.c>
            Require all denied
        </IfModule>
    '''
}
